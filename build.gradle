buildscript {
    ext.kotlin_version = "1.3.10"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "me.champeau.gradle.jmh" version "0.4.8"
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'me.champeau.gradle.jmh'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        groovy {
            srcDir 'test'
            srcDir 'integration-test'
        }
    }
    jmh {
        java {
            srcDir 'jmh-benchmarks'
        }
    }
}

mainClassName = 'optic.lua.Main'

jar {
    baseName = 'optic-lua-lib'
}

shadowJar {
    baseName = 'optic-lua'
    classifier = null
    dependencies {
        exclude(dependency('org.codehaus.groovy::'))
        exclude(dependency('junit::'))
        exclude(dependency('org.junit.platform::'))
        exclude(dependency('org.testng::'))
        exclude(dependency('jline::'))
        exclude(dependency('org.apache.ant::'))
        exclude(dependency('org.opentest4j::'))
        exclude(dependency('org.apiguardian::'))
        exclude(dependency('com.thoughtworks.qdox::'))
        exclude(dependency('com.sun.mail::'))
    }
}

run {
    standardInput = System.in
}

tasks.withType(JavaExec) {
    // pass any environment variables beginning with "optic." to java process, for example: "gradle run -Doptic.shell=true"
    for (def entry : System.getProperties())
        if (entry.key.toString().startsWith("optic."))
            systemProperty(entry.key.toString(), entry.value)
}

dependencies {
    // At the start I ran into some issues (see https://github.com/melix/jmh-gradle-plugin/issues/111)
    // Currently it works fine for me, but just in case if it ever happens again, try uncommenting this:
    // compile group: 'org.openjdk.jmh', name: 'jmh-generator-reflection', version: '1.18'
    // Also, try running on a fresh gradle daemon!
    compile 'com.google.code.gson:gson:2.8.5'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.3.0-alpha4'
    compile group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    compile 'com.github.bkiers:lua-parser:-SNAPSHOT'
    compile 'org.codehaus.groovy:groovy-all:2.5.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.codehaus.janino:janino:3.0.11'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
